\begin{chapter}[Sequence Interface]
\label{ch:seq-interface}
\begin{preamble}
The \sml{SEQUENCE} signature is a comprehensive interface for a
persistent sequence type.
% We use a number of notational conventions which
% can be seen <a href="../../../resources/notation/sequence.html">here</a>.
% For example, we write $|s|$ and $s[i]$ for the length and $i^\text{th}$
% element of $s$, respectively.
\end{preamble}

\begin{section}[Summary]
\begin{gram}
\begin{verbatim}
signature SEQUENCE =
sig
  type 'a t
  type 'a seq = 'a t
  type 'a ord = 'a * 'a -> order
  datatype 'a listview = NIL | CONS of 'a * 'a seq
  datatype 'a treeview = EMPTY | ONE of 'a | PAIR of 'a seq * 'a seq

  exception Range
  exception Size

  val nth : 'a seq -> int -> 'a
  val length : 'a seq -> int
  val toList : 'a seq -> 'a list
  val toString : ('a -> string) -> 'a seq -> string
  val equal : ('a * 'a -> bool) -> 'a seq * 'a seq -> bool

  val empty : unit -> 'a seq
  val singleton : 'a -> 'a seq
  val tabulate : (int -> 'a) -> int -> 'a seq
  val fromList : 'a list -> 'a seq

  val rev : 'a seq -> 'a seq
  val append : 'a seq * 'a seq -> 'a seq
  val flatten : 'a seq seq -> 'a seq

  val filter : ('a -> bool) -> 'a seq -> 'a seq
  val map : ('a -> 'b) -> 'a seq -> 'b seq
  val zip : 'a seq * 'b seq -> ('a * 'b) seq
  val zipWith : ('a * 'b -> 'c) -> 'a seq * 'b seq -> 'c seq

  val enum : 'a seq -> (int * 'a) seq
  val filterIdx : (int * 'a -> bool) -> 'a seq -> 'a seq
  val mapIdx : (int * 'a -> 'b) -> 'a seq -> 'b seq
  val update : 'a seq * (int * 'a) -> 'a seq
  val inject : 'a seq * (int * 'a) seq -> 'a seq

  val subseq : 'a seq -> int * int -> 'a seq
  val take : 'a seq -> int -> 'a seq
  val drop : 'a seq -> int -> 'a seq
  val splitHead : 'a seq -> 'a listview
  val splitMid : 'a seq -> 'a treeview

  val iterate : ('b * 'a -> 'b) -> 'b -> 'a seq -> 'b
  val iteratePrefixes : ('b * 'a -> 'b) -> 'b -> 'a seq -> 'b seq * 'b
  val iteratePrefixesIncl : ('b * 'a -> 'b) -> 'b -> 'a seq -> 'b seq
  val reduce : ('a * 'a -> 'a) -> 'a -> 'a seq -> 'a
  val scan : ('a * 'a -> 'a) -> 'a -> 'a seq -> 'a seq * 'a
  val scanIncl : ('a * 'a -> 'a) -> 'a -> 'a seq -> 'a seq

  val sort : 'a ord -> 'a seq -> 'a seq
  val merge : 'a ord -> 'a seq * 'a seq -> 'a seq
  val collect : 'a ord -> ('a * 'b) seq -> ('a * 'b seq) seq
  val collate : 'a ord -> 'a seq ord
  val argmax : 'a ord -> 'a seq -> int

  val $ : 'a -> 'a seq
  val % : 'a list -> 'a seq
end
\end{verbatim}
\end{gram}
\end{section}

\begin{section}[Types]

\begin{gram}
\begin{verbatim}
type 'a t
type 'a seq = 'a t
\end{verbatim}
The abstract sequence type \sml{'a t} has elements of type \sml{'a}. The alias
\sml{'a seq} is for readability.
\end{gram}

\begin{gram}
\begin{verbatim}
type 'a ord = 'a * 'a -> order
\end{verbatim}
An alias, for readability.
\end{gram}

\begin{gram}
\begin{verbatim}
datatype 'a listview = NIL | CONS of 'a * 'a seq
\end{verbatim}
View a sequence as though it were a list. See~\ref{gr:splitHead}.
\end{gram}

\begin{gram}
\begin{verbatim}
datatype 'a treeview = EMPTY | ONE of 'a | PAIR of 'a seq * 'a seq
\end{verbatim}
View of a sequence as though it were a tree with data at the leaves.
This is largely a syntactic convenience for 2-way divide-and-conquer algorithms
on sequences. See~\ref{gr:splitMid}.
\end{gram}

\end{section}

\begin{section}[Exceptions]

\begin{gram}
\begin{verbatim}
exception Range
\end{verbatim}
The \sml{Range} exception is raised whenever an invalid index into a sequence is used.
\end{gram}

\begin{gram}
\begin{verbatim}
exception Size
\end{verbatim}
The \sml{Size} exception is raised whenever a function is given a negative size.
\end{gram}

\end{section}

\begin{section}[Values]

\begin{gram}[nth]
\label{gr:nth}
\begin{verbatim}
val nth : 'a seq -> int -> 'a
\end{verbatim}
\sml{nth s i} evaluates to $s[i]$, the $i^\text{th}$ element of $s$.
Raises \sml{Range} if $i$ is out-of-bounds.
\end{gram}

\begin{gram}[length]
\label{gr:length}
\begin{verbatim}
val length : 'a seq -> int
\end{verbatim}
\sml{length s} evaluates to $|s|$, the number of elements in $s$.
\end{gram}

\begin{gram}[toList]
\label{gr:toList}
\begin{verbatim}
val toList : 'a seq -> 'a list
\end{verbatim}
Produces an index-preserving list representation of a sequence.
\end{gram}

\begin{group}
\begin{gram}[toString]
\label{gr:toString}
\begin{verbatim}
val toString : ('a -> string) -> 'a seq -> string
\end{verbatim}
\sml{toString f s} produces a string representation of $s$, where each
element of $s$ is converted to a string via $f$.
\end{gram}
\begin{example}
\sml{toString Int.toString (fromList [1,2,3])} evaluates to \sml{"<1,2,3>"}.
\end{example}
\end{group}

\begin{gram}[equal]
\label{gr:equal}
\begin{verbatim}
val equal : ('a * 'a -> bool) -> 'a seq * 'a seq -> bool
\end{verbatim}
\sml{equal f (s, t)} returns whether or not $s$ and $t$ contain equal elements
in the same order. Individual element pairs are compared for equality with $f$.
\end{gram}

\begin{gram}[empty]
\label{gr:empty}
\begin{verbatim}
val empty : unit -> 'a seq
\end{verbatim}
Construct an empty sequence.
\end{gram}

\begin{gram}[singleton]
\label{gr:singleton}
\begin{verbatim}
val singleton : 'a -> 'a seq
\end{verbatim}
\sml{singleton x} evaluates to $\seq{x}$.
\end{gram}

\begin{gram}[tabulate]
\label{gr:tabulate}
\begin{verbatim}
val tabulate : (int -> 'a) -> int -> 'a seq
\end{verbatim}
\sml{tabulate f n} evaluates to the length-$n$ sequence where the $i^\text{th}$
element is given by $f(i)$. Raises \sml{Size} if $n < 0$.
\end{gram}

\begin{gram}[fromList]
\label{gr:fromList}
\begin{verbatim}
val fromList : 'a list -> 'a seq
\end{verbatim}
Produces an index-preserving sequence from a list.
\end{gram}

\begin{gram}[rev]
\label{gr:rev}
\begin{verbatim}
val rev : 'a seq -> 'a seq
\end{verbatim}
\sml{rev s} reverses the indexing of a sequence. That is, \sml{nth (rev s) i}
is equivalent to \sml{nth s (length s - i - 1)}.
\end{gram}

\begin{gram}[append]
\label{gr:append}
\begin{verbatim}
val append : 'a seq * 'a seq -> 'a seq
\end{verbatim}
Concatenate two sequences.
\end{gram}

\begin{group}
\begin{gram}[flatten]
\label{gr:flatten}
\begin{verbatim}
val flatten : 'a seq seq -> 'a seq
\end{verbatim}
Concatenate many sequences into one, in the order they are given.
\end{gram}
\begin{example}
Flattening $\seq{\seq{1,2},\seq{},\seq{3}}$ results in $\seq{1,2,3}$.
\end{example}
\end{group}

\begin{gram}[filter]
\label{gr:filter}
\begin{verbatim}
val filter : ('a -> bool) -> 'a seq -> 'a seq
\end{verbatim}
\sml{filter p s} evaluates to the subsequence of $s$ which contains every
element satisfying the predicate $p$.
\end{gram}

\begin{gram}[map]
\label{gr:map}
\begin{verbatim}
val map : ('a -> 'b) -> 'a seq -> 'b seq
\end{verbatim}
\sml{map f s} applies $f$ to each element of $s$. It is logically equivalent
to \sml{tabulate (f o nth s) (length s)}.
\end{gram}

\begin{gram}[zip]
\label{gr:zip}
\begin{verbatim}
val zip : 'a seq * 'b seq -> ('a * 'b) seq
\end{verbatim}
\sml{zip (s, t)} evaluates to a sequence of length $\min(|s|,|t|)$ whose $i^\text{th}$
element is the pair $(s[i], t[i])$.
\end{gram}

\begin{gram}[zipWith]
\label{gr:zipWith}
\begin{verbatim}
val zipWith : ('a * 'b -> 'c) -> 'a seq * 'b seq -> 'c seq
\end{verbatim}
\sml{zipWith f (s, t)} is logically equivalent to \sml{map f (zip (s, t))}.
\end{gram}

\begin{gram}[enum]
\label{gr:enum}
\begin{verbatim}
val enum : 'a seq -> (int * 'a) seq
\end{verbatim}
\sml{enum s} evaluates to a sequence where each element is paired with its
index. It is logically equivalent to
\sml{tabulate (fn i => (i, nth s i)) (length s)}.
\end{gram}

\begin{gram}[filterIdx]
\label{gr:filterIdx}
\begin{verbatim}
val filterIdx : (int * 'a -> bool) -> 'a seq -> 'a seq
\end{verbatim}
Similar to~\ref{gr:filter}, but also provides the
index of each element to the predicate.
\sml{filterIdx f s} is logically equivalent to
\sml{map (fn (\_, x) => x) (filter f (enum s))}.
\end{gram}

\begin{gram}[mapIdx]
\label{gr:mapIdx}
\begin{verbatim}
val mapIdx : (int * 'a -> 'b) -> 'a seq -> 'b seq
\end{verbatim}
Similar to~\ref{gr:map}, but also provides the index of each element.
\sml{mapIdx f s} is logically equivalent to \sml{map f (enum s)}.
\end{gram}

\begin{gram}[update]
\label{gr:update}
\begin{verbatim}
val update : 'a seq * (int * 'a) -> 'a seq
\end{verbatim}
\sml{update (s, (i, x))} evaluates to the sequence whose $i^\text{th}$ element
is $x$, and whose other elements are unchanged from $s$. If $i$ is out-of-bounds,
it raises \sml{Range}, otherwise it is logically equivalent to
\sml{tabulate (fn j => if i = j then x else nth s j) (length s)}.
\end{gram}

\begin{group}
\begin{gram}[inject]
\label{gr:inject}
\begin{verbatim}
val inject : 'a seq * (int * 'a) seq -> 'a seq
\end{verbatim}
\sml{inject (s, u)} produces a new sequence where, for each $(i,x) \in u$,
the $i^\text{th}$ element of $s$ has been replaced with $x$. If there are
multiple updates specified
at the same index, then all but one of them are ignored non-deterministically.
Raises \sml{Range} if any $(i,\_) \in u$ is out-of-bounds.
\end{gram}
\begin{note}
When all indices in $u$ are distinct, \sml{inject (s, u)} is logically
equivalent to \sml{iterate update s u}.
\end{note}
\end{group}

\begin{gram}[subseq]
\label{gr:subseq}
\begin{verbatim}
val subseq : 'a seq -> int * int -> 'a seq
\end{verbatim}
\sml{subseq s (i, n)} evaluates to the contiguous subsequence of $s$ starting
at index $i$ with length $n$. Raises \sml{Size} if $n < 0$. Raises \sml{Range}
if the subsequence is out-of-bounds.
\end{gram}

\begin{gram}[take]
\label{gr:take}
\begin{verbatim}
val take : 'a seq -> int -> 'a seq
\end{verbatim}
\sml{take s n} takes the prefix of $s$ of length $n$. It is logically
equivalent to \sml{subseq s (0, n)}.
\end{gram}

\begin{gram}[drop]
\label{gr:drop}
\begin{verbatim}
val drop : 'a seq -> int -> 'a seq
\end{verbatim}
\sml{drop s n} drops the prefix of $s$ of length $n$. It is logically equivalent
to \sml{subseq s (n, length s - n)}.
\end{gram}

\begin{gram}[splitHead]
\label{gr:splitHead}
\begin{verbatim}
val splitHead : 'a seq -> 'a listview
\end{verbatim}
\sml{splitHead s} evaluates to \sml{NIL} if $s$ is empty, and otherwise is
logically equivalent to \sml{CONS (nth s 0, drop s 1)}.
\end{gram}

\begin{gram}[splitMid]
\label{gr:splitMid}
\begin{verbatim}
val splitMid : 'a seq -> 'a treeview
\end{verbatim}
\sml{splitMid s} evaluates to \sml{EMPTY} is $s$ is empty, \sml{ONE x} if
$s = \seq{x}$, and \sml{PAIR (l, r)} otherwise where $l$ and $r$ are non-empty and their
concatenation is $s$. The sizes of $l$ and $r$ are implementation-defined.
\end{gram}

\begin{group}
\begin{gram}[iterate]
\label{gr:iterate}
\begin{verbatim}
val iterate : ('b * 'a -> 'b) -> 'b -> 'a seq -> 'b
\end{verbatim}
\sml{iterate f b s} computes the iteration of $f$ on $s$ with left-association,
using $b$ as the base case. It is logically equivalent to
\[
  f(\ldots f(f(b, s[0]), s[1]) \ldots, s[|s|-1])
\]
\end{gram}
\begin{note}
When $s$ is empty, it returns $b$.
\end{note}
\begin{example}
\sml{iterate op+ 13 (fromList [1,2,3])} evaluates to \sml{19}.
\end{example}
\end{group}

\begin{gram}[iteratePrefixes]
\label{gr:iteratePrefixes}
\begin{verbatim}
val iteratePrefixes : ('b * 'a -> 'b) -> 'b -> 'a seq -> 'b seq * 'b
\end{verbatim}
\sml{iteratePrefixes f b s} is logically equivalent to
\[
  \sml{(tabulate (fn i => iterate f b (take s i)) (length s), iterate f b s)}.
\]
That is, it produces the iteration of $f$ for each prefix of $s$.
\end{gram}

\begin{group}
\begin{gram}[iteratePrefixesIncl]
\label{gr:iteratePrefixesIncl}
\begin{verbatim}
val iteratePrefixesIncl : ('b * 'a -> 'b) -> 'b -> 'a seq -> 'b seq
\end{verbatim}
Similar to~\ref{gr:iteratePrefixes}, except that the $i^\text{th}$ prefix of
\sml{iteratePrefixesIncl f b s} is inclusive of $s[i]$, rather than exclusive.
It is logically equivalent to
\[
  \sml{tabulate (fn i => iterate f b (take s (i+1)) (length s)}.
\]
\end{gram}
\begin{note}
The return type of~\ref{gr:iteratePrefixesIncl} is slightly different than
\ref{gr:iteratePrefixes}.
\end{note}
\end{group}

\begin{group}
\begin{gram}[reduce]
\label{gr:reduce}
\begin{verbatim}
val reduce : ('a * 'a -> 'a) -> 'a -> 'a seq -> 'a
\end{verbatim}
\sml{reduce f b s} evaluates to $b$ when $s = \seq{}$,
$x$ when $s = \seq{x}$, and otherwise is logically equivalent to
\[
  \sml{f (reduce f b l, reduce f b r)}
\]
where $l = \sml{take s (n div 2)}$ and $r = \sml{drop s (n div 2)}$.
\end{gram}
\begin{note}
\sml{reduce f b s} is logically equivalent to \sml{iterate f b s} when
$f$ is associative and $b$ is a corresponding identity.
\end{note}
\end{group}

\begin{gram}[scan]
\label{gr:scan}
\begin{verbatim}
val scan : ('a * 'a -> 'a) -> 'a -> 'a seq -> 'a seq * 'a
\end{verbatim}
For an associative function $f$ and corresponding identity $b$, \sml{scan f b s}
is logically equivalent to \sml{iteratePrefixes f b s}.
\end{gram}

\begin{gram}[scanIncl]
\label{gr:scanIncl}
\begin{verbatim}
val scanIncl : ('a * 'a -> 'a) -> 'a -> 'a seq -> 'a seq
\end{verbatim}
For an associative function $f$ and corresponding identity $b$, \sml{scanIncl f b s}
is logically equivalent to \sml{iteratePrefixesIncl f b s}.
\end{gram}

\begin{gram}[sort]
\label{gr:sort}
\begin{verbatim}
val sort : 'a ord -> 'a seq -> 'a seq
\end{verbatim}
\sml{sort c s} reorders the elements of $s$ with respect to the comparison
function $c$. The output is \emph{stable}: any two elements considered equal
by $c$ will appear in the same relative order in the output as they were
in the input.
\end{gram}

\begin{gram}[merge]
\label{gr:merge}
\begin{verbatim}
val merge : 'a ord -> 'a seq * 'a seq -> 'a seq
\end{verbatim}
For sequences $s$ and $t$ already sorted with respect to $c$,
\sml{merge c (s, t)} is logically equivalent to \sml{sort c (append (s, t))}.
\end{gram}

\begin{group}
\begin{gram}[collect]
\label{gr:collect}
\begin{verbatim}
val collect : 'a ord -> ('a * 'b) seq -> ('a * 'b seq) seq
\end{verbatim}
\sml{collect c s} takes a sequence $s$ of key-value pairs, deduplicates the
keys, sorts them with respect to $c$, and pairs each unique key with all values
that were originally associated with it in $s$. The resulting value-sequences
retain their relative ordering from $s$.
\end{gram}
\begin{example}
Collecting $\seq{(3,7),(2,6),(1,8),(3,5)}$ produces
$\seq{(1,\seq{8}), (2, \seq{6}), (3,\seq{7,5})}$.
\end{example}
\end{group}

\begin{gram}[collate]
\label{gr:collate}
\begin{verbatim}
val collate : 'a ord -> 'a seq ord
\end{verbatim}
\sml{collate c} produces an ordering on sequences, derived lexicographically
from $c$.
\end{gram}

\begin{gram}[argmax]
\label{gr:argmax}
\begin{verbatim}
val argmax : 'a ord -> 'a seq -> int
\end{verbatim}
\sml{argmax c s} produces the \emph{index} of the maximal value in $s$
with respect to $c$. Raises \sml{Range} when $s$ is empty.
\end{gram}

\begin{gram}[\$]
\begin{verbatim}
val $ : 'a -> 'a seq
\end{verbatim}
An alias for~\ref{gr:singleton}.
\end{gram}

\begin{gram}[\%]
\begin{verbatim}
val % : 'a list -> 'a seq
\end{verbatim}
An alias for~\ref{gr:fromList}.
\end{gram}

\end{section}

\end{chapter}
